#!/usr/bin/env zsh

# <UserConfig>
	# Where do you clone your HubSpot projects
	if [[ -z $HS_PROJECTS_DIR ]]; then
		# default if not in env
    HS_PROJECTS_DIR="$HOME/repos"
	else
		# pull from env if you already export this somewhere
    HS_PROJECTS_DIR=${HS_PROJECTS_DIR}
	fi

	# Allow node up to ~8gigs (new m1 macbooks have ~64GB of ram so this is pretty reasonable)
	# Doesn't use env because you probably don't want to use this much memory for every node process
	# Note: might be too much if you are on an intel macbook
	if [[ -z $FLEXO_NODE_ARGS ]]; then
		# default if not in env
    FLEXO_NODE_ARGS="NODE_OPTIONS='--max_old_space_size=8192'"
	else
		# pull from env if you already export this somewhere
    FLEXO_NODE_ARGS=${FLEXO_NODE_ARGS}
	fi
#</UserConfig>

RED='\033[0;31m'
PURPLE='\033[0;35m'
BOLD_PURPLE='\033[1;35m'
CYAN='\033[0;36m'
NC='\033[0m' # No Color


if [[ -z $(command -v gum) ]]; then
	message=$(cat <<-EOF
	  ╔════════════════════════════════╗
  ║                                ║
  ║         Flexo requires         ║
  ║  github.com/charmbracelet/gum  ║
  ║      ${CYAN}brew install gum${NC}          ║
  ║                                ║
  ╚════════════════════════════════╝
EOF
)
  echo $message;
  return;
fi

_FLEXO="[${BOLD_PURPLE}Flexo${NC}]"

flexo_print_error() {
	printf "$_FLEXO Error: ${RED}%s${NC}\n" $1
}

flexo_print_info() {
  printf "$_FLEXO Running \x22${CYAN}%s${NC}\x22 \n" $1
}

flexo_print_help() {
	printf "$_FLEXO runs bend processes for you\n"
	printf "Flexo will always pass \x22${CYAN}$FLEXO_NODE_ARGS${NC}\x22 to any bend commands it spawns\n"
	printf "\nCommands\n"

	local cmds=$(cat <<EOF
${BOLD_PURPLE}flexo${NC}\tChoose from a list of your repos in ${CYAN}\$HS_PROJECTS_DIR${NC} (currently set to '${HS_PROJECTS_DIR}')
${BOLD_PURPLE}flexo repo-one repo-two${NC}\tanalogous to \x22${CYAN}$FLEXO_NODE_ARGS bend reactor serve repo-one repo-two --update --ts-watch${NC}\x22 Takes any number of repo names
EOF
	)
	echo $cmds | column -ts $'\t'

	printf "\nEnvironment Variables\n"
	local envs=$(cat <<EOF
${BOLD_PURPLE}HS_PROJECTS_DIR${NC}\t Where are your hubspot repos located? (use \$HOME for the ~ directory, using a literal '~' does wonky stuff. export HS_PROJECTS_DIR="\$HOME/my_projects_dir")
${BOLD_PURPLE}FLEXO_NODE_ARGS${NC}\t define the arguments to pass to node. The default is \x22NODE_OPTIONS='--max_old_space_size=8192'\x22
EOF
	)
	echo $envs | column -ts $'\t'
}

flexo_run_with_bend() {
	local cmd=$(printf "%s bend reactor serve %s --update --ts-watch\n" $FLEXO_NODE_ARGS $1)
  flexo_print_info $cmd
  eval $cmd
}

flexo_exec() {
	cd $HS_PROJECTS_DIR;

	if [[ $1 == "-h" ]] || [[ $1 == "help" ]]; then
		flexo_print_help
		return;
	fi

	# $* is a single string of all arguments passed
	# $# is the number of arguments passed
	if [[ $# -ge 1 ]]; then
		flexo_run_with_bend "$*"
		return;
	fi

  # select what to run from all repos
  local project_directories=$(command ls -Sd */ | cut -f1 -d '/' | sort -r)
  local len=$(echo $project_directories | wc -l | xargs)
  if [[ $len -gt 25 ]]; then
    len=25
  fi
  local selected_repos=$(echo $project_directories | gum choose --no-limit --height "$len" --cursor.foreground "#86A")
  if [[ -z $selected_repos ]]; then
    flexo_print_error "No repos selected (use spacebar to select, hit return to confirm selection)"
    return;
  fi
  local args=$(echo $selected_repos)
  flexo_run_with_bend $args
  return;
}

flexo_exec "$@"

